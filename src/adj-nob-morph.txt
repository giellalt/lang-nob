! =============================
! Sublexica for adjective roots
! =============================
! a1  god         god         godt        gode
! a2  norsk       norsk       norsk       norske
! a3  ekte        ekte        ekte        ekte
! a4  oppskjørtet oppskjørtet oppskjørtet oppskjørtede/oppskjørtete
! a5  makaber     makaber     makabert    makabre
!     lunken      lunken      lunkent     lunkne




! Basic paradigms
! ===============

LEXICON a1 ! 
 +A: positive ;
 +A: a-neuter ;
 +A: grade-e ;

LEXICON a2lig
 ig:ig a2 ; ! herlig
 ig+Nynorsk:eg a2 ; ! herleg
   
LEXICON a2 ! 
 +A: positive ;
 +A: a-no-neuter ;
 +A: grade-C ;

LEXICON a3 ! 
 +A: positive ;
 +A: a-no-neuter ;

LEXICON a4 ! oppskjørtet
 +A:   positive-indef ;
 +A:   positive-def ;
 +A:X6 positive-def ;
 +A:   a-no-neuter ;
 +A:   grade-e ;   !oppskjørtete
 +A:X6 grade-e ;   !oppskjørtede

LEXICON a5 ! overlegen
 +A:X1 positive ;
 +A: a-neuter ;
 +A:X1 grade-e ;

LEXICON aV ! ny, fri
+A+Sg+Indef+MF: # ;
+A+Sg+Indef+Neu:tt # ;
+A+Sg+Def:e # ;
+A+Pl+Indef:e # ;
+A+Pl+Def:e # ;
+A+Comp:ere # ;
+A+Superl:est # ;
+A+Superl+Def:este # ;

+A+Comp+Nynorsk:are # ;
+A+Superl+Nynorsk:ast # ;
+A+Superl+Def+Nynorsk:aste # ;


LEXICON EGEN ! legen
 +A:nX1 positive ;
 +A+Pos+Neu+Sg+Indef:t # ;
 +A:nX1 grade-e ;


! Sublexica
! =========

LEXICON positive
 positive-indef ;
 positive-def ;

LEXICON positive-indef
 R ;
 +Pos+Msc+Sg+Indef: # ;
 +Pos+Fem+Sg+Indef: # ;
 +Pos+MF+Sg+Indef: # ;

LEXICON positive-def
 +Pos+Sg+Def:%>e # ;
 +Pos+Sg+Def:%>e # ;
 +Pos+Sg+Def:%>e # ;
 +Pos+Pl+Def:%>e # ;

LEXICON a-neuter ! fint
 +Pos+Neu+Sg+Indef:%>Q3t # ;
 
LEXICON a-no-neuter ! vennlig
 R ;
 +Pos+Neu+Sg+Indef: # ;

LEXICON grade-e
 +Comp:%>ere # ;
 +Superl+Indef:%>est # ;
 +Superl+Def:%>este # ;

LEXICON grade-C
 +Comp:%>ere # ;
 +Comp:%>are # ; ! +Nynorsk
 +Comp+Nynorsk:%>are # ;
 +Superl+Indef:%>st # ;
 +Superl+Def:%>ste # ;



! Exceptional

LEXICON FLER
fler+A+Comp+Pl+Indef:fler # ;
flere+A+Comp+Pl+Indef:flere # ;
flere+A+Superl+Pl+Def:fleste # ;
flere+A+Superl+Pl+Indef:flest # ;

LEXICON GAMMEL
!gammel+A+Sg+Indef+Msc:gammel # ;
!gammel+A+Sg+Indef+Fem:gammel # ;
gammel+A:gammel R ;
gammel+A+Sg+Indef+MF:gammel # ;
gammel+A+Sg+Indef+Neu:gammelt # ;
gammel+A+Sg+Def:gamle # ;
gammel+A+Pl+Indef:gamle # ;
gammel+A+Pl+Def:gamle # ;
gammel+A+Comp:eldre # ;
gammel+A+Superl:eldst # ;
gammel+A+Superl+Def:eldste # ;

LEXICON GOD
!god+A+Sg+Indef+Msc:god # ;
!god+A+Sg+Indef+Fem:god # ;
god+A:god R ;
god+A+Sg+Indef+MF:god # ;
god+A+Sg+Indef+Neu:godt # ;
god+A+Sg+Def:gode # ;
god+A+Pl+Indef:gode # ;
god+A+Pl+Def:gode # ;
god+A+Comp:bedre # ;
god+A+Superl:best # ;
god+A+Superl+Def:beste # ;

LEXICON LITEN
liten+A+Sg+Indef+Msc:liten # ;
liten+A+Sg+Indef+Fem:lita # ;
liten+A+Sg+Indef+Neu:lite # ;
liten+A+Sg+Def:lille # ;
liten+A+Sg+Def:vesle # ;
liten+A+Pl+Indef:små # ;
liten+A+Pl+Def:små # ;
liten+A+Comp:mindre # ;
liten+A+Superl:minst # ;
liten+A+Superl+Def:minste # ;

LEXICON NY
ny aV ;


LEXICON STOR
!stor+A+Sg+Msc+Indef:stor # ;
!stor+A+Sg+Fem+Indef:stor # ;
stor+A:stor R ;
stor+A+Sg+MF+Indef:stor # ;
stor+A+Sg+Neu+Indef:stort # ;
stor+A+Sg+Def:store # ;
stor+A+Pl+Indef:store # ;
stor+A+Pl+Def:store # ;
stor+A+Comp+Indef:større # ;
stor+A+Superl+Indef:størst # ;
stor+A+Superl+Def:største # ;

LEXICON UNG
ung+A:ung R ;
ung+A+Sg+Indef+Msc:ung # ;
ung+A+Sg+Indef+Fem:ung # ;
ung+A+Sg+Indef+Neu:ungt # ;
ung+A+Sg+Def:unge # ;
ung+A+Pl+Indef:unge # ;
ung+A+Pl+Def:unge # ;
ung+A+Comp:yngre # ;
ung+A+Superl:yngst # ;
ung+A+Superl+Def:yngste # ;



