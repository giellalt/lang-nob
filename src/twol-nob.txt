Alphabet
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å

 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç

 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð

 o:ø u:y a:e

 %#:0 %>:0 

 X1:0 X2:0 X3:0 X4:0 X5:0 X6:0 X7:0 X8:0 X9:0
 Q1:0 Q2:0 Q3:0 Q4:0 Q5:0 Q6:0 Q7:0 Q8:0 Q9:0
 Y1:0 Y2:0 Y3:0 Y4:0 Y5:0 Y6:0 Y7:0 Y8:0 Y9:0 
 Z1:0 Z2:0 Z3:0 Z4:0 Z5:0 Z6:0 Z7:0 Z8:0 Z9:0 ;

Sets
 Vow = a e i o u y æ ø å ;
 Cns = b c d f g h j k l m n p q r s t v w x z ;
 LNR = l n r ;
 Dummy = X1 X2 X3 X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9 
 Z1 Z2 Z3 Z4 Z5 Z6 Z7 Z8 Z9 ;  ! %+ 

Rules

! Epenthetic deletions
! --------------------

"Contraction"
 e:0 <=> Cns: _ LNR X1: %>: ( e | a ) ;

"Delete schwaa" ! h a r e:0 %> e r
 e:0 <=> Vow ( Cns:* ) _ ( Dummy:* ) %>:0 [ Vow | Cns Cns ] ;

"Delete Consonant before schwaa Deletion"
 Cx:0 <=> Cx _ e: LNR X1: %>:0 e ;
      where Cx in ( d s ) ;

"Delete Plural Consonant"
 r:0 <=> _ %>:0 X3: ;
! sparepengar : sparepengane

"Delete m"
 m:0 <=> _ X4: [ #: | %>:0 s ] ;

"Umlaut"
 Vx:Vy <=> _ Cns:* ( e:0 ) Z1: ;
 where Vx in ( u o a )
       Vy in ( y ø e )
       matched ;
! bok : bøker

!"m deletion"
! m:0 <=> Vow: _ m #: ;


"um Deletion 1"
 u:0 <=> _ m: X5: %>:0 ;

"um Deletion 2"
 m:0 <=> u: _ X5: %>:0 ;


! Insertion rules
! ---------------

"Insert t in passives"
 0:t <=> _ e s Q1: ;

"Insert d in passives"
 0:d <=> _ e s Q1: ;

! Clitics
! -------

"Clitic after s-final"
 s:' <=> s %>:0 _ ;
! huss -> hus'
