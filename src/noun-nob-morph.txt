! ====================== !
! Bokmål noun morphology !
! ====================== !

! Declension classes
! Main types, from Bokmålsordboka

! f1  bru         brua        bruer       bruene
! f2  pumpe       pumpa       pumper      pumpene
! m1  stol        stolen      stoler      stolene
!     bakke       bakken      bakker      bakkene
!     pumpe       pumpen      pumper      pumpene
! m2  lærer       læreren     lærere      lærerne
! m3  bever       beveren     bevere      beverne
!                             bevre(r)    bevrene
! n1  slott       slottet     slott       slotta/slottene
! n2  eple        eplet       epler       epla/eplene
!     salt        saltet      salter      salta/saltene
! n3  kontor      kontoret    kontor      kontora
!                             kontorer    kontorene
!     høve        høvet       høve/høver  høva/høvene

! Subtypes, mainly from Finsk-norsk ordbok, also system-specific

! x  unclassified, to m1 by default
! mX indecl
! m1sg sg only
! m1b dam
! m1b fe, komite
! miV sko pl. sko
! m3V meter pl. meter
! KOLLEGA kollegaer, kolleger
! KONTO kontoer, konti
! RADIUS radiuser, radii
! BROR brødre
! FAR fedre
! DATTER døtre
! MANN menn
! f1b skam
! f1X bok pl. bøker
! f1V mus, pl. mus
! n1b rom pl. rom
! n1s sg only
! n2b program pl. programmer
! n2c kontor pl. kontor, kontorer
! n2s mørke, not pl.
! n3b lager def. lageret
! n3c fe, feet
! n4b faktum pl fakta
! LEKSIKON leksikon, pl. leksika




! Basic paradigms
! ===============

! Masculines
! ----------

LEXICON x 
!+X: m1 ; !Temporarily excluding the xX
 m1 ;     ! change line back if needed.


LEXICON mX
 +N+Msc: K ;

LEXICON m1 ! dag, hane
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl: er-ene ;

LEXICON m1sg ! bruk
  +N+Msc+Sg: 0-en ;

LEXICON m1X
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl:Z1 er-ene ;

LEXICON m1b ! dam
 +N+Msc+Sg:mX4 0-en ;
 +N+Msc+Pl:m er-ene ;

LEXICON m1c ! fe, komite
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl: er-ene ;

LEXICON m1V ! sko
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl: 0-ene ;

LEXICON m2 ! lærer
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl: e-ne ;

LEXICON m3 ! onkel
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl:X1 er-ene ; ! X1 deletes the vowel -e-

LEXICON m3V ! meter
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl:X1 0-ene ;

LEXICON KOLLEGA
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl:X2 er-ene ; ! delete a, kolleger
 +N+Msc+Pl: er-ene ;   ! keep a, kollegaer 

LEXICON KONTO
 +N+Msc+Sg: 0-en K ;
 +N+Msc+Pl:X2i 0-ene ; ! delete o
 +N+Msc+Pl: er-ene ; ! Regular pl

LEXICON RADIUS
 +N+Msc+Sg:%>us 0-en ;
 +N+Msc+Sg+Def:%>en K ; ! short def
 +N+Msc+Pl: er-ene ; ! Regular pl

LEXICON BROR
 bror+N+Msc+Sg:bror 0-en ;
 bror+N+Msc+Pl:brødr e-ene ; 

LEXICON FAR
 far+N+Msc+Sg:far 0-en ;
 far+N+Msc+Pl:fedr e-ene ; 


LEXICON DATTER
 +N+Fem+Sg+Indef:datter # ;
 +N+Fem+Sg+Def:datra # ;
 +N+Fem+Sg+Indef:døtre # ;
 +N+Fem+Sg+Def:døtrene # ;

LEXICON MANN
 +N+Msc+Sg: 0-en ;
 +N+Msc+Pl:Z1 0-ene ;

LEXICON indeclm !mX, eigentleg.
 +N+Msc+Indcl: # ;

! Feminines
! ---------

LEXICON fx ! Former vx, awaiting classification.
 f1 ;

LEXICON f1 ! myr, krone
 +N+Fem+Sg: 0-a ;
 +N+Msc+Sg: en ; ! myren !^C^
 +N+Fem+Pl: er-ene ;

LEXICON f1b ! skam
 +N+Fem+Sg:mX4 0-a ;
 +N+Msc+Sg:m en ; ! myren !^C^
 +N+Fem+Pl:m er-ene ;

LEXICON f1X ! bok
 +N+Fem+Sg: 0-a ;
 +N+Msc+Sg: en ; ! boken !^C^
 +N+Fem+Pl:Z1 er-ene ;

LEXICON f1V ! mus
 +N+Fem+Sg: 0-a ;
 +N+Msc+Sg: en ; ! musen !^C^
 +N+Fem+Pl: 0-ene ;
 
LEXICON f3 ! lever
 +N+Fem+Sg:X1 0-a ;
 +N+Msc+Sg: en ; ! levren !^C^
 +N+Fem+Pl:X1 er-ene ;
 

! Neuters
! -------

LEXICON nX ! styrbord
 +N+Neu: K ;

LEXICON n1 ! hus
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl: 0-ene-a ;

LEXICON n1b ! rom
 +N+Neu+Sg:mX4 0-et ;
 +N+Neu+Pl:m 0-ene-a ;

LEXICON n1sg ! yr
 +N+Neu+Sg: 0-et ;

LEXICON n2 ! eple, salt
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl: er-ene ;

LEXICON n2b ! program
 +N+Neu+Sg:mX4 0-et ;
 +N+Neu+Pl:m 0-er-ene ;

LEXICON n2c ! album, kontor
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl: 0-er-ene ;

LEXICON n2s ! mørke
 +N+Neu+Sg: 0-et ;

LEXICON n3 ! middel
 +N+Neu+Sg:X1 0-et ;
 +N+Neu+Pl:X1 er-ene ;

LEXICON n3b ! lager
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl:X1 e-ene ;

LEXICON n3c ! fe
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl: 0-ne ;

LEXICON n4 ! kriterium
 +N+Neu+Sg:X5 0-et ;
 +N+Neu+Pl:X5 er-ene ;

LEXICON n4b ! faktum
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl:X5 a-aene ;

LEXICON LEKSIKON ! leksikon
 +N+Neu+Sg: 0-et ;
 +N+Neu+Pl:leksik a-aene ;

LEXICON KLÆR
 +N+Neu:klær 0-ne ;


! Lexica for definiteness declension
! ==================================

LEXICON a-aene
 +Indef:%>a K ;
 +Def:%>aene K ;

LEXICON e-ne
 +Indef:%>e K ;
 +Def:%>ne K ;

LEXICON e-ene
 +Indef:%>e K ;
 +Def:%>ene K ;
 
LEXICON er-ene
 +Indef:%>er K ;
 +Def:%>ene K ;

LEXICON 0-a
 +Indef: K ;
 +Def:%>a K ;

LEXICON 0-en
 +Indef: K ;
 +Def:%>en K ;

LEXICON 0-ene
 +Indef: K ;
 +Def:%>ene K ;

LEXICON 0-er-ene
 +Indef: K ;
 +Indef:%>er K ;
 +Def:%>ene K ;

LEXICON 0-ne
 +Indef: K ;
 +Def:%>ne K ;

LEXICON 0-et
 +Indef: K ;
 +Def:%>et K ;

LEXICON 0-ene-a
 +Indef: K ;
 +Def:%>ene K ;
 +Def:%>a K ;

LEXICON en
 +Indef: K ; ! en dør, sukk.
 +Def:%>en K ;



