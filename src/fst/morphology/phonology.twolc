!! # Morphophonological rules for Bokmål

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-nob/blob/main/src/fst/phonology.twolc) 

!! ## Sets and definitions

!! ### Alphabet
Alphabet

!! We declare both the a-å letters and all other possible letters.
 a b c d e f g h i j k l m n o p q r s t u v w x y z æ ø å !!≈ * **@CODE@** 
 á é ó ú í à è ò ù ì ä ë ö ü ï â ê ô û î ã ý þ ñ ð ß ç     !!≈ * **@CODE@**
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z Æ Ø Å !!≈ * **@CODE@**
 Á É Ó Ú Í À È Ò Ù Ì Ä Ë Ö Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð         !!≈ * **@CODE@**
 '   !!≈ * **@CODE@** This is the apostrophe
  l7:l    !!≈ * **@CODE@** This **l** not deleted by **t**
  e7:e    !!≈ * **@CODE@** This **e** not deleted by vowel-initial suffixes (ide - ideen)
! o:ø u:y a:e g:0 e:0 m:0 r:0 t:0 u:0
! t:d
! 0:t
! 0:d
! s:'

!! ### Boundary symbols

!! Morpheme boundaries and escaped quotes - do not delete in twolc,
!! they will be converted to zero/the real thing at a later stage.

 %#         !!≈ * **@CODE@**
 %>         !!≈ * **@CODE@**
 %<         !!≈ * **@CODE@**
 %|»      !!  * **%\|»**
 «%|      !!  * **%\|«**
 %[%<%]   !!≈ * **@CODE@**
 %[%>%]   !!≈ * **@CODE@**
 «7
 »7

!! ### Morphophonological triggers

!! These symbols cause the twolc rules to work.

!! #### Triggers for nominal rules
X1:0  !!≈ * **@CODE@** = Epenthetic vowel moden:modne
X2:0  !!≈ * **@CODE@** = ...
X3:0  !!≈ * **@CODE@** = Plural r deletion -er, -ene
X4:0  !!≈ * **@CODE@** = dumm:dum Delete m
X5:0  !!≈ * **@CODE@** = um deletion, faktum:fakta
X6:0  !!≈ * **@CODE@** = t:d weakening, oppskjørtet:oppskjørtede

!! #### Trigers for verbal rules
Q1:0  !!≈ * **@CODE@** = Passive
Q2:0  !!≈ * **@CODE@** = ...
Q3:0  !!≈ * **@CODE@** = Verb vowel and geminate deltion kalte, fylte

!! #### Triggers for common rules (both for N and V)
Z1:0  !!≈ * **@CODE@** = o:ø, a:e Umlaut
Z2:0  !!≈ * **@CODE@** = protects vowel

!! #### Nynorsk trigger
%^NYNAG:0 ;  !!≈ * **@CODE@** Trigger for Nynorsk dictionary forms.


! the chapter "Rule-variables" must under the M4 regime:
! To be removed when compiling on Xerox twolc:

!htwolc Rule-variables

        ! All rule-variables, that are used, should be declared.
        ! If this isn't done, annoying warning-messages will be
        ! issued (otherwise the grammar is constructed as it
        ! should be)

!htwolc        Cx Cy Cz Vx Vy ;

!! ### Sets

Sets 
 Vow = a e i o u y æ ø å e7 ;                        !!≈ * **@CODE@**
 Cns = b c d f g h j k l m n p q r s t v w x z l7 ;  !!≈ * **@CODE@**
 LNR = l n r ;                                       !!≈ * **@CODE@**
 Dummy = X1 X2 X3 X4 X5 X6 Q1 Q2 Q3  Z1 Z2 %^NYNAG ; !!≈ * **@CODE@**

!! ## Rule section

!! This section shows the twolc rules and the tests used to check whether they work

Rules 


 
!! ### Umlaut

"Umlaut Rule"
!! **@RULENAME@**  for *bok : bøker* etc. It shifts the vowels *u, o, a, å* to *y, ø, e, e*, respectively when **Z1** is found after the stem.
 Vx:Vy <=> _ Cns:* ( e:0 ) Z1: ;
 where Vx in ( u o a å )
       Vy in ( y ø e e )
       matched ;



!! ### Vowel deletions rules
! --------------------

"Epenthetic Deletion Rule"
!! **@RULENAME@**  is actually 3 rules in one: 1) it deletes -e- in *moden : modne* etc, 2) it deletes the stem -e in *hare + -er* and 3) it delets suffix -e in *ærlig + est > ærligst*
 e:0 <=>      Cns:  _ LNR X1: %> ( e | a ) ;
         Vow  Cns:* _  [Dummy:* - Z2:]  %> [ Vow | Cns Cns ] ;
                    _  (X4:) %> Q3: ;
            i g %>  _ s t ;

!! *Tests:* (star denotes negativ test, test that is supposed to fail)
!!€ søsterX1>a
!!£ søst00>ra
!!€ teaterX1>et
!!€ teat0r0>et
!!€ modenX1>e
!!€ mod0n0>e
!!€ reparere>Q3te
!!€ reparer0>0te
!!$ modenX1>e
!!$ moden0>e
!!€ hare>er
!!€ har0>er
!!€ viktig>est
!!€ viktig>0st
!!€ presentere%>Q3t
!!€ presenter0>0t

"Delete foreign vowel Rule"
!! **@RULENAME@**  for deleting final a or o in words like *kollega : kolleger*. Trigger symbol to the right is **X2**.
 Vx:0 <=> _ X2:  ;
   where Vx in (a o) ;

!! *Tests:*
!!€ kollegaX2>er
!!€ kolleg00>er



!! ### Consonant deletion

"Consonant shortening before deletion Rule"
!! **@RULENAME@**  
 Cx:0 <=> Vow Cx _ e: LNR X1: %> ( e | a ) ;
          where Cx in ( b d v s ) ;

!! *Tests:*
!!€ sikkerX1>e
!!€ sik00r0>e
!!€ trusselX1>er
!!€ trus00l0>er


"Geminate deletion in front of -t and -d Rule"
!! **@RULENAME@** deletes: 1) before **Q3 and d or t** (*kaller:kalte*) 2) before passive **Q1 t** (*lykkes:lyktes*) and 3) before epenthetic **-e-** and **l, n** or **r** (*sikker:sikre*)
 Cx:0 <=> Vow Cx _ (e:0)         %> Q3: [d|t] ; ! kaller:kalte
          Vow Cx _ %> :t e s  Q1:  ; ! lykkes:lyktes
          Vow Cx _  e:   LNR X1: %> ( e | a ) ;  ! sikker:sikre
      where Cx in ( f k p s l m n g ) ;

!! *Tests:*
!!€ kalle>Q3te
!!€ kal00>0te
!!€ lykk0esQ1
!!€ lyk0tes0
!!€ all>Q3t
!!€ al0>0t
!!€ bygge>Q3de
!!€ byg00>0de



!"Delete schwaa" ! h a r e:0 %> e r
! e:0 <=> Vow ( Cns:* ) _ ( Dummy:* - Z2: ) %> [ Vow | Cns Cns ] ;


"Delete r Rule"
!! **@RULENAME@** deletes **r** in plural *-er* to get *-er + -ne* = plural *-ene* 
 r:0 <=>   _ %> X3: ;
         r _ e: LNR X1: %> ( e | a ) ;
! sparepenger : sparepengene

"Delete m Rule"
!! **@RULENAME@** for *kam:kammen*, here we delete the second m when word-final.  
 m:0 <=>    _      X4: [ %> s | #: (Q3:) ] ; ! kammX4:kam, gjemme
          m _ e:        %> Q3: t ; ! gjemt
!          m _ e:        %> Q3: X4: ; ! gjem
	  m _ (e:)  X4:  %> Q3:  ; ! gjemmeX4>Q3
	  m _ (e:) (X4:) %> Q3: t ; ! stummX4>Q3t
         u: _ X5:   %> ;
          m _ e: LNR X1: %> ( e | a ) ; ! gammel:gamle
         


"um Deletion 1 Rule"
!! **@RULENAME@** (um Deletion 2 is now part of the *Delete m Rule*)
 u:0 <=> _ m: X5: %> ;

!! *Tests:*
!!€ museumX5>er
!!€ muse000>er

!"um Deletion 2" ! now m rule
! m:0 <=> u: _ X5: %> ;


"t weakening Rule"
!! **@RULENAME@**  
 t:d <=> [Cns:|Vow:] _ X6:0 %> e ;

!! *Tests:*
!!€ oppskjørtetX6>e
!!€ oppskjørted0>e
 
"Double t deletion Rule"
!! **@RULENAME@**  
 t:0 <=>   Cns t %> _  .#. ;  ! svart
           Vow t    _  e:  LNR X1: %> (e|a) ;      ! sikker:sikre
           Vow t    _ (e:0)        %> Q3:0 [d|t] ; ! kaller:kalte

!! *Tests:*
!!€ svart>t
!!€ svart>0

!" inf-e"
! e:0 <=> _ %> Q3: ;


!! ### Insertion rules
! ---------------

!"Insert t in passives Rule"
! **@RULENAME@**  
! 0:t <=> _ e s Q1: ;

!"Insert d in passives"
! 0:d <=> _ e s Q1: ;


!! ### Compound rule


"compund Rule"
Cx:0 <=> Cx _ #: #: Cx ;
     where Cx in (b d f g k l m n p r s t v ) ;

!! *Tests:*
!!€ grammatikk##kontroll
!!€ grammatik000kontroll



!! ### Clitics
! -------

"Clitic after s-final Rule"
!! **@RULENAME@**  for changing the so-called *genitive -s* to **'** for s-final stems: *huss -> hus'*
 s:' <=> s %> _ ;





!! ### Nynorsk dictionary rules

"Change -er stem to -ar in Nynorsk"
!! **@RULENAME@**  
 e:a <=> _ r %^NYNAG: ;

!! This rule is for dictionary use only. The idea is to be able to click on words in a Nynorsk text and get translation to North Sámi. Therefore, the Bokmål analyser is able to give an analysis to Nynorsk words as well. The Nynorsk-only forms are removed from all other transducers than the `-dict` transducer.


!!€ a
!!€ b

!! Test to have an error
!!$ a
!!$ b
