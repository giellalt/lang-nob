! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no


LEXICON ACRONOUN_cons  
 ACRONOUN ;

LEXICON ACRONOUN_vow  
 ACRONOUN ;

LEXICON UNIT   !!= * **@CODE@**  As acro, but without paradigm
+N+ABBR: # ;

LEXICON ACRO_ACCRA  !!= * **@CODE@** 
+N+Prop+ACR: acroconnector ;
!+N+Prop:%> ACCRADECL ;

LEXICON ACRO_BERN
+N+Prop+ACR: acroconnector ;
!+N+Prop:%> BERN-UCASE ;

LEXICON ACRO_LONDON
+N+Prop+ACR: acroconnector ;
!+N+Prop:%> LONDON-UCASE ;

LEXICON ACRO_NYSTØ
+N+Prop+ACR: acroconnector ;
!+N+Prop:%> NYSTØ-OBL ;

LEXICON ACRO_cons 
 ACRO ;    

LEXICON ACRO_vow 
 ACRO ;    


LEXICON ACRO 
 +N+Prop+ACR: acroconnector ; 
 
LEXICON DYN_ACRO_cons 
 +Use/Circ+Use/SpellNoSugg: DYN_ACRO ;    

LEXICON DYN_ACRO_vow 
 +Use/Circ+Use/SpellNoSugg: DYN_ACRO ;    
   

LEXICON DYN_ACRO
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector ;


LEXICON ACRONOUN              ! Here we add the POS tag
 +N+ACR: acroconnector ;

LEXICON acroconnector_vow
 acroconnector ;

! Språkrådet:
! «I bøyde former av forkortelser o.l. og i avledninger bruker vi bindestrek slik:
! pc-en, pc-er, pc-ene (men: en pc’s prosessor, se artikkelen om apostrof), AUF-er»
! Vidare:
! «Dersom genitivs-s skal legges til et forbokstavord i en sammenheng der det ellers bare er brukt store bokstaver, 
! kan vi enten skrive liten genitivs-s eller bruke apostrof.
! NRKs KORRESPONDENTER eller NRK’S KORRESPONDENTER»
LEXICON acroconnector     ! Here comes a set of possible symbols to ...
                acronull ; ! SgNom only in sma
          :%>%- acrosuff ; ! Hyphen is correct: BH-en
 +Err/Orth:%>%: acrosuff ; ! BH:en
 +Err/Orth:%>   acrosuff ; ! BHen
 +Err/Orth:%>'  acrosuff ; ! BH'en
 +Err/Orth:%>’  acrosuff ; ! BH’en
 +Err/Orth:%>%´ acrosuff ; ! BH´en
 +Err/Orth:%>%` acrosuff ; ! BH`en
          :%>   acropossessive_lower ; ! NRKs
          :%>’  acropossessive_upper ; ! NRK’S
 +Err/Orth:%>   acropossessive_upper ; ! NRKS
 +Err/Orth:%>’  acropossessive_lower ; ! NRK’s
 +Err/Orth:%>%- acropossessive ; ! NRK-s
 +Err/Orth:%>%: acropossessive ; ! NRK:s
 +Err/Orth:%>'  acropossessive ; ! NRK's
 +Err/Orth:%>%´ acropossessive ; ! NRK´s
 +Err/Orth:%>%` acropossessive ; ! NRK`s
               RHyph    ; ! Acros compound with a hyphen
 +Err/Orth:     R ;        ! Without hyphen, not normative:  NRK23

LEXICON acronull
+Msc+Sg+Indef: K ;

LEXICON acrosuff
+Msc+Sg+Indef:    K ;
  +Msc+Sg+Def:en  K ;
+Msc+Pl+Indef:er  K ;
  +Msc+Pl+Def:ene K ;

LEXICON acropossessive
+Msc+Sg+Indef+Clt:s K ;
+Msc+Sg+Indef+Clt:S K ;

LEXICON acropossessive_lower
+Msc+Sg+Indef+Clt:s K ;

LEXICON acropossessive_upper
+Msc+Sg+Indef+Clt:S K ;